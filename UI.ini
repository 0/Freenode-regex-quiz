[script]
n0=on *:TEXT:!submission *:#regex-bot: {
n1=  if ($2 isnum 1-) {
n2=    msg $chan $iif($read($regexDir $+ \submission.txt,n,$2),[By: $gettok($v1,1,32) @ $asctime($gettok($v1,2,32)) $+ ]: $gettok($v1,3-,32),No such submission ID!)
n3=  }
n4=  else if ($2 == 0) {
n5=    msg $chan Submission count: $lines($regexDir $+ \submission.txt)
n6=  }
n7=}
n8=on $*:TEXT:/^!(commands|help|login|register|regexquiz|stop|submit|info|try|task|newtry|lasttry|mypattern|alltasks)(\s+|$)/Si:?: {
n9=  var %target = $iif($chan,$v1,$nick)
n10=
n11=  if (!$findUserFromHost($address($nick,5)) && !$istok(commands help login register regexquiz submit stop,$regml(1),32)) { 
n12=    sendTimer %target You need to 3!register before you can use me! If you have registered, use 3!login For further information use 3!help.
n13=    return 
n14=  }
n15=
n16=  if ($regml(1) == login) || ($regml(1) == register) {
n17=    var %file = $+($regexDir,users\,$2,.ini)
n18=    if ($2 && $3) {
n19=      if ($regml(1) == login) {
n20=        if ($readini(%file,n,info,password) == $md5($3)) {
n21=          if ($readini(%file,n,info,host) != $address($nick,5)) {
n22=            writeini -n %file info host $address($nick,5)
n23=            sendTimer %target Welcome3 $nick $+(,$chr(40),or should I say,$chr(32),$2,?,$chr(41),!)
n24=            sendTimer %target Your host has been updated to $+(3,$address($nick,5),.) If your host changes again, just re-login!
n25=          }
n26=          else {
n27=            sendTimer %target 3You are already logged in $nick $+ , no need to do it twice!
n28=          }
n29=        }
n30=        else {
n31=          sendTimer %target 4Username or password incorrect. Are you looking to !register ?
n32=        }
n33=      }
n34=      else {
n35=        if (!$readini(%file,n,info,password)) {
n36=          if (!$findUserFromHost($address($nick,5))) {
n37=            ; the function above will return the username if it finds the ip is not unique, thus the !
n38=            if ($2 isalnum) {
n39=              if ($address($nick,5)) {
n40=                writeini -n %file info username $2
n41=                writeini -n %file info password $md5($3)
n42=                writeini -n %file info host $address($nick,5)
n43=                writeini -n %file info reached 1
n44=                write -n hosts.txt $address($nick,5)
n45=                sendTimer %target Congratulations $nick $+ ! You are now registered. Your IP $+($chr(40),4,$address($nick,5),,$chr(41)) has been stored. If your IP changes you will need to re-login, and you do so by typing 3!login <username> <password>. The indrotuction should begin shortly. Have fun!
n46=                regexQuiz %target
n47=              }
n48=              else {
n49=                sendTimer %target 4I was unable to aquire your IP for some reason. Please try again later
n50=              }
n51=            }
n52=            else {
n53=              sendTimer %target 3You may only use the following chracters in your username: [a-zA-Z0-9]
n54=            }
n55=          }
n56=          else {
n57=            sendTimer %target It would seem that you have previously registered with the username $+("4,$findUserFromHost($address($nick,5)),".) If you have forgotten your password contact an administrator!
n58=          }
n59=        }
n60=        else {
n61=          sendTimer %target 2This username is already taken, please try another.
n62=        }
n63=      }
n64=    }
n65=    else {
n66=      sendTimer %target Syntax: ! $+ $regml(1) <username> <password>
n67=    }
n68=  }
n69=  else if ($regml(1) == help || $regml(1) == commands) {
n70=    sendTimer %target Welcome to the Freenode regex quiz. The purpose of this quiz is to help you learn regex quick and easy. We will be here with you all the way to help you. If you have any question feel free to ask them in channel ( $+ #regex $+ ).
n71=    sendTimer %target Tutorial: 12http://www.regular-expressions.info/tutorialcnt.html
n72=    sendTimer %target The commands are: !help, !login <username> <password>, !register <username> <password>, !regexquiz for the intro, !task for your current task, !lasttry the last pattern you tried, !mypattern <level> shows your solution for that level, $&
n73=      !newtry <level> <pattern> to modify a solution, !alltasks shows all tasks, !submit <idea> send some feedback, !stop to stop your sending queue
n74=    sendTimer %target !explain /regex/ will explain it bit by bit, !regex <text> /regex/ to test your pattern, !regex <text> s/regex/replacement/ to run a regub,  !credits The credits
n75=    sendTimer %target Admins: !info [-u] <nick> [level|last] Shows nick's solutions for a certain lvl/his last try. The 3-u switch will conduct a username only search.
n76=  }
n77=  else if ($regml(1) == regexquiz) {
n78=    regexQuiz %target
n79=  }
n80=  else if ($regml(1) == stop) {
n81=    $+(.timer,%target,*) off
n82=    sendTimer %target 2Queue halted.
n83=  }
n84=  else if ($regml(1) == submit) {
n85=    if ($5) {
n86=      sendTimer %target 3Thank you. We have received your submission and will review it shortly.
n87=      write $+($regexDir,\submission.txt) $nick $ctime $2-
n88=    }
n89=    else {
n90=      sendTimer %target 3I'm sure you have more to say than that ;-)!
n91=    }
n92=  }
n93=  else if ($regml(1) == info) {
n94=    if ($2) {
n95=      var %u = $iif($2 == -u,$3,$2), %t = $iif($2 == -u,$4,$3)
n96=      if ($findUserFromHost($address($2,5)) || ($2 == -u && $4 && $isfile($+($regexDir,users\,$3,.ini)))) {
n97=        var %f = $findUserFromHost($address($nick,5)), %q = $iif($2 == -u,$3,$findUserFromHost($address($2,5)))
n98=        %t = $replace($iif(%t isnum || %t == last,%t,last),last,$regexUser(%q,info,reached))
n99=        if ($regexUser(%f,info,reached) >= $regexUser(%q,info,reached)) {
n100=          if (!$regexUser(%f,task $+ %t,shortest)) {
n101=            if ($regexUser(%q,task $+ %t,shortest)) {
n102=              if ($len($regexUser(%f,task $+ %t,shortest)) >= $len($regexUser(%q,task $+ %t,shortest))) {
n103=                sendTimer %target 3 $+ %u LEVEL $regexUser(%q,info,reached) Sol  %t $+ : $regexUser(%q,task $+ %t,shortest)) 15( $+ $len($regexUser(%q,task $+ %t,shortest))) chars)
n104=              }
n105=              else {
n106=                sendTimer %target 4Sorry,3 %q $+ 's4 solution is shorter and thus I will not show it to you! 
n107=              }
n108=            }
n109=            else {
n110=              if ($regexUser(%q,task $+ %t,last)) {
n111=                sendTimer %target LVL: $regexUser(%q,info,reached) $+ , $regexTask(%t,description) Last try: $regexUser(%q,task $+ %t,last)
n112=                sendTimer %target Fails on: $regexUser(%q,task $+ %t,fail)
n113=              }
n114=              else {
n115=                sendTimer %target 3 $+ %u LEVEL $regexUser(%f,info,reached) has not $iif($regexUser(%q,info,reached) >= %t,started on,reached) 3task %t $+  yet
n116=              }
n117=            }
n118=          }
n119=          else {
n120=            sendTimer %target 4You have not solved 3task %t $+  4yet
n121=          }
n122=        }
n123=        else {
n124=          sendTimer %target 4You have not solved 3task %t $+  4yet
n125=        }
n126=      }
n127=      else {
n128=        sendTimer %target $+(3,%u,4) not found. Syntax: !info <nickname|username> [level|last]
n129=      }
n130=    }
n131=    else {
n132=      sendTimer %target 3You need to specifiy a nick- or username!
n133=    }
n134=  }
n135=  else if ($regml(1) == task) {
n136=    if ($2 isnum 1-) {
n137=      if ($2 <= $regexTasks) {
n138=        var %f = $findUserFromHost($address($nick,5))
n139=        if ($2 <= $regexUser(%f,info,reached)) {
n140=          if ($regexUser(%f,task1,attempts)) {
n141=            sendTimer %target 3Task $2 of $regexTasks $+ : $+([,$regexTask($2,title),]) $regexTask($2,description)
n142=          }
n143=          else {
n144=            regexQuiz %target
n145=          }
n146=        }
n147=        else {
n148=          sendTimer %target 4You have not reached task $2 yet!
n149=        }
n150=      }
n151=      else {
n152=        sendTimer %target 2Task $2 does not exist yet! Perhaps you could !submit it?
n153=      }
n154=    }
n155=    else {
n156=      var %f = $regexUser($findUserFromHost($address($nick,5)),info,reached)
n157=      sendTimer %target 3Task %f of $regexTasks $+ : $+([,$regexTask(%f,title),]) $regexTask(%f,description)
n158=    }
n159=  }
n160=  else if ($regml(1) == try) {
n161=    var %f = $findUserFromHost($address($nick,5))
n162=    if ($regexUser(%f,info,reached) >= $regexTasks) {
n163=      .signal validate %f $v1 6
n164=      return
n165=    }
n166=    else {
n167=      if ($2) noop $validateRegex(%f, $regexUser(%f,info,reached), $2-)  
n168=      else sendTimer %target 3You forgot to input a /regular expression/!
n169=    }
n170=  }
n171=  else if ($regml(1) == newtry) {
n172=    if ($2 isnum 1-) {
n173=      if ($2 <= $regexTasks) {
n174=        var %f = $findUserFromHost($address($nick,5))
n175=        if ($2 <= $regexUser(%f,info,reached)) {
n176=          noop $validateRegex(%f, $2, $3-) 
n177=        }
n178=        else {
n179=          sendTimer %target 4You have not reached task $2 yet!
n180=        }
n181=      }
n182=      else {
n183=        sendTimer %target 2Task $2 does not exist yet! Perhaps you could !submit it?
n184=      }
n185=    }
n186=    else {
n187=      sendTimer %target 3You need to provide a task number! Syntax is !newtry <task> /regex/
n188=    }
n189=  }
n190=  else if ($regml(1) == lasttry) {
n191=    ; if $2 ... (admin shit)
n192=    var %a = $regexUser($findUserFromHost($address($nick,5)),info,reached)
n193=    if ($regexUser($findUserFromHost($address($nick,5)),task $+ %a,last)) {
n194=      sendTimer %target 3Last Try: $v1
n195=    }
n196=    else {
n197=      sendTimer %target 3Last Try: 4Not Found. Did you change log in?
n198=    }
n199=  }
n200=  else if ($regml(1) == mypattern) {
n201=    if ($2 isnum 1-) {
n202=      if ($2 <= $regexTasks) {
n203=        var %f = $findUserFromHost($address($nick,5))
n204=        if ($2 < $regexUser(%f,info,reached)) {
n205=          sendTimer %target 3Task $2 $+  $regexTask($2,title) $+  $regexUser(%f,task $+ $2,shortest) $+(14,$chr(40),$len($regexUser(%f,task $+ $2,shortest)),$chr(32),chars,$chr(41)) $+(12,$chr(40),shortest:,$chr(32),$gettok($regexTask($2,shortest),2,32),$chr(32),chars,$chr(41))
n206=        }
n207=        else {
n208=          sendTimer %target 4You have not completed task $2 yet!
n209=        }
n210=      }
n211=      else {
n212=        sendTimer %target 2Task $2 does not exist yet! Perhaps you could !submit it?
n213=      }
n214=    }
n215=    else {
n216=      sendTimer %target 3You need to input a task number to retreive the regex for!
n217=    }
n218=  }
n219=  else if ($regml(1) == alltasks) {
n220=    ;$regexUser(test123,task2,shortest)
n221=    var %f = $findUserFromHost($address($nick,5))
n222=    if ($regexUser(%f,task1,shortest)) {
n223=      var %i = 1
n224=      while ($regexUser(%f,task $+ %i,shortest)) {
n225=        sendTimer %target 3Task %i $+  $regexTask(%i,title) $+  $regexUser(%f,task $+ %i,shortest) $+(14,$chr(40),$len($regexUser(%f,task $+ %i,shortest)),$chr(32),chars,$chr(41)) $+(12,$chr(40),shortest:,$chr(32),$gettok($regexTask(%i,shortest),2,32),$chr(32),chars,$chr(41))
n226=        inc %i
n227=      }
n228=    }
n229=  }
n230=}
n231=alias -l regexQuiz {
n232=  sendTimer $1 Welcome to the 7Freenode regex quiz.
n233=  sendTimer $1 The objective of the game is to provide a valid solution to every regex task given. All tasks are related to PCRE.
n234=  sendTimer $1 In a moment, you will receive your next task. You may take as long as you need to solve it. When you're ready to provide a solution message me with the text !try /pattern/
n235=  sendTimer $1 Some other tasks are intended to replace text, where the quiz will evaluate your pattern using $!!regsub(). In such tasks, use !try s/pattern/substitution/[flags]
n236=  sendTimer $1 I will perform a few tests to the pattern you provide for each task. If you submit an incorrect solution, I'll try to point out the error or the text that makes it fail (sometimes I use colors - please don't strip them).
n237=  sendTimer $1 You won't receive the next task until you successfully provide a solution. It may take you a very long time to finish, take it as a learning experience and please don't ask for help on topics explained in the tutorial: 12http://www.regular-expressions.info/tutorialcnt.html
n238=  sendTimer $1 Other Commands: !task to read your current task again. !lasttry to see your last try in the current level. !mypatterns to see your solutions for each level. !submit <idea> if you have an idea for a new task or a comment. Or !help
n239=  sendTimer $1 If you still need more help with regex syntax, ask in #regex @ Freenode. But, please, keep in mind that channel is for regex help. Quiz questions are considered spam there, and you'll be banned if you post a quiz solution.
n240=  sendTimer $1 Good luck!
n241=  if ($regexUser($findUserFromHost($address($nick,5)),info,reached) < 2) {
n242=    sendTimer $1 Task 1 of $regexTasks $+ . $+([,$regexTask(1,title),]) $regexTask(1,description)
n243=  }
n244=}
n245=alias -l findUserFromHost {
n246=  var %a = 1
n247=  while ($findfile($+($regexDir,users\),*.ini,%a,1)) {
n248=    var %u = $left($nopath($v1),-4)
n249=    if ($readini($v1,n,info,host) == $1) {
n250=      return %u
n251=    }
n252=    inc %a
n253=  }
n254=  return
n255=}
n256=alias sendTimer {
n257=  var %i = 0, %j = 1
n258=  while ($timer(%i)) {
n259=    if ($regex($v1,/^ $+ $1 $+ =\w+/i)) inc %j
n260=    ;inc %j $iif(%j > 1,2,1)
n261=    inc %i
n262=  }
n263=  $+(.timer,$1,=,$r(a,z),$r(0,9),$r(a,z),$r(0,9),$r(a,z),$r(0,9),$r(a,z)) 1 %j msg $1 $2-
n264=}
n265=alias username2nick return $ial($regexUser($1,info,host)).nick
n266=alias regexFile return $mircdir $+ regex/users.ini
n267=alias regexDir return $mircdir $+ regex\
